# Data source para obter versão mais recente se não especificada
data "aws_rds_engine_version" "default" {
  engine  = var.engine
  version = var.engine_version != "" ? var.engine_version : null
}

locals {
  engine_version        = var.engine_version != "" ? var.engine_version : data.aws_rds_engine_version.default.version
  port                  = var.engine == "aurora-postgresql" ? 5432 : 3306
  family                = data.aws_rds_engine_version.default.parameter_group_family
  cluster_identifier    = "${var.name_prefix}-aurora-${var.suffix}"
  final_snapshot_identifier = "${var.name_prefix}-aurora-final-snapshot-${formatdate("YYYY-MM-DD-hhmm", timestamp())}"
}

# DB Subnet Group
resource "aws_db_subnet_group" "aurora" {
  name       = var.allow_external_access ? "${var.name_prefix}-aurora-subnet-group-public" : "${var.name_prefix}-aurora-subnet-group"
  subnet_ids = var.database_subnet_ids

  lifecycle {
    create_before_destroy = true
  }

  tags = merge(
    var.tags,
    {
      Name = var.allow_external_access ? "${var.name_prefix}-aurora-subnet-group-public" : "${var.name_prefix}-aurora-subnet-group"
    }
  )
}

# Security Group para Aurora
resource "aws_security_group" "aurora" {
  name        = "${var.name_prefix}-aurora-sg"
  description = "Security group para Aurora cluster"
  vpc_id      = var.vpc_id

  # Permite acesso da VPC
  ingress {
    description = "PostgreSQL/MySQL from VPC"
    from_port   = local.port
    to_port     = local.port
    protocol    = "tcp"
    cidr_blocks = [data.aws_vpc.main.cidr_block]
  }

  # Permite acesso do Glue Connection (via Security Group)
  ingress {
    description     = "Acesso do Glue Connection"
    from_port       = local.port
    to_port         = local.port
    protocol        = "tcp"
    security_groups = var.glue_security_group_ids
  }

  # Permite acesso do Bastion Host (via Security Group)
  ingress {
    description     = "Acesso do Bastion Host"
    from_port       = local.port
    to_port         = local.port
    protocol        = "tcp"
    security_groups = [var.bastion_security_group_id]
  }

  # Saída liberada para qualquer destino
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(
    var.tags,
    {
      Name = "${var.name_prefix}-aurora-sg"
    }
  )
}


# Data source para VPC (para obter CIDR)
data "aws_vpc" "main" {
  id = var.vpc_id
}

# Cluster Parameter Group
resource "aws_rds_cluster_parameter_group" "aurora" {
  name        = "${var.name_prefix}-aurora-params"
  family      = local.family
  description = "Parameter group para Aurora cluster ${var.name_prefix}"

  parameter {
    name  = "timezone"
    value = "UTC"
  }

  tags = merge(
    var.tags,
    {
      Name = "${var.name_prefix}-aurora-params"
    }
  )
}

# Aurora Cluster
resource "aws_rds_cluster" "aurora" {
  cluster_identifier              = local.cluster_identifier
  engine                          = var.engine
  engine_version                  = local.engine_version
  database_name                   = var.database_name
  master_username                 = var.master_username
  master_password                 = var.master_password
  db_subnet_group_name            = aws_db_subnet_group.aurora.name
  db_cluster_parameter_group_name = aws_rds_cluster_parameter_group.aurora.name
  vpc_security_group_ids          = [aws_security_group.aurora.id]
  
  # Backup e Maintenance
  backup_retention_period         = var.backup_retention_period
  preferred_backup_window         = var.preferred_backup_window
  preferred_maintenance_window    = var.preferred_maintenance_window
  
  # Encryption
  storage_encrypted               = true
  enabled_cloudwatch_logs_exports = var.engine == "aurora-postgresql" ? ["postgresql"] : ["mysql", "audit", "error", "general", "slowquery"]
  
  # Snapshot
  skip_final_snapshot             = var.skip_final_snapshot
  final_snapshot_identifier       = var.skip_final_snapshot ? null : local.final_snapshot_identifier
  
  # High Availability
  deletion_protection             = false
  
  tags = merge(
    var.tags,
    {
      Name = local.cluster_identifier
    }
  )
}

# Aurora Instances
resource "aws_rds_cluster_instance" "aurora" {
  count              = var.instance_count
  identifier         = "${local.cluster_identifier}-${count.index + 1}"
  cluster_identifier = aws_rds_cluster.aurora.id
  instance_class     = var.instance_class
  engine             = aws_rds_cluster.aurora.engine
  engine_version     = aws_rds_cluster.aurora.engine_version

  publicly_accessible = var.allow_external_access

  lifecycle {
    create_before_destroy = true
  }

  tags = merge(
    var.tags,
    {
      Name = "${local.cluster_identifier}-${count.index + 1}"
    }
  )
}

